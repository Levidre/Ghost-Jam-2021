//---------------------------Mouse Functions--------------------------
//Zarla : 
//As you've probably guessed, this .dic file has to do with mouse functions. This includes the petting feature and what happens when you double click your ghost.
//As a note, this .dic file isn't strictly necessary - I'm pretty sure you can just clear the file and it won't hurt your ghost any, it'll just make it so you can't pet or double click them. And if you're interested in only petting or double clicking one character, all you have to do is delete the chunks of code for the other character you want to be uninvolved. However, I'm assuming most people making ghosts want to make their ghosts pettable and clickable, so this will cover the messages for both.

//Make sure to read the walkthrough page (http://www.ashido.com/ukagaka/walkthrough.html) for more details about how to properly format ghost dialogue!
//I will reiterate that you should be editing these in Notepad++, and in particular, you should set the Language to C (or Java I guess) to properly highlight all the text. It will make things A LOT EASIER for you, trust me! It will also help you keep track of your brackets in case you forget some. If you click on a line with a bracket in Notepad++, it should highlight where the other bracket ends, or turn purple if there's no finishing bracket (which you should definitely fix!)
//It will also highlight my commented lines in green, so you'll know where they are. Any line beginning with // is a commented line that the code will not read. It's just for your reference!


//--OnMouseMove
//Zarla : This is a bunch of code determining who the mouse is over. Don't touch any of it.

OnMouseMove
 {
  	if nowmode != "QUIZ"
 	{
		//Zarla : ---- reference3 determines the character in question
		if reference3 == 0 //Zarla : if the character is 0, aka the main character
		{
			//Zarla : ---- it will run the mouse functions for that character
			MouseMoveSakura
		}
		else //Zarla : if the character isn't 0, then it must be the side character.
		{
			//Zarla : ---- this will run the mouse functions for the side character.
			MouseMoveKero
		}
	}
 }
 
 //*********************Advanced User Info*******************************
//Zarla : 
//These text boxes are meant for those who have an interest in expanding or using more complicated functions in their ghost. Also for my own reference in the future since I plan on using this template too, haha. If you have no interest in any of this and just want to keep things as simple as possible, feel free to skip these boxes.

//If you want to have more than two characters at any point, like for example how I added Charger with my Hunter/Smoker ghost, and you want to be able to interact with that character like you can with the others, you will have to add in that character to OnMouseMove above. You'll need to know the p[] you've assigned your third character to, which in Charger's case was 3. You can assign your third character any p you want, just be consistent about using the same p[] everytime. I touch on \p a bit more in the walkthrough.
//So anyway, as you can see above, OnMouseMove determines who the user is touching, so you'll just have to add in an elseif checking for reference3 == 3, or reference3 == 4, or whatever p number is you're using. More on elseif in the walkthrough and in bootend.dic. After that, you'll just need to copy and paste the other mouse functions below and change the name as you need (for example, MouseMoveCharger, MouseDoubleClickCharger). 

//Note though that the extra character will not be a hard-coded character like 0 and 1! You're basically tricking the program into thinking there's another character, and some functions in things like menu.dic and aitalk.dic won't work quite right if you put in a value other than 0 or 1, and you'll need to be careful about making sure to remove the third character when you're done. But luckily for you, this won't be a problem with any of the mouse functions as far as I'm aware, and you can fill them out for the third character just like you would for the two main ones.
//***********************************************************************


 //--MouseMoveSakura
 //Zarla : The petting function for your main character. Remember how it was referred to as sakura in some of the descript.txt and install.txt files?
 
MouseMoveSakura
 {
	if reference4 != "" //ignore
 	{
 		stroke++ //Zarla : if you move the mouse over the right areas, the stroke count will go up.
 		if stroke >= 40 //Zarla : if the stroke count is above a certain number, the petting dialogue will kick in. You can change this number to make the petting take longer or shorter if you want. FLELEs have it set to 50 for some reason, which makes it take longer than this.
		{
 			stroke = 0 //Zarla : resetting the stroke count
			if reference4 == "Head" //Zarla : do you remember the names of the areas you set up for your collisions in surfaces.txt? This is where they come into play! This is where you'd put in the name of your collision, such as head, face, horns, tail, ears, whatever. As mentioned in surfaces.txt, you can add as many collisions to a pose as you want, as long as you make sure to add them all in here so they can actually be used.
 			{
				
				//Zarla : Replace these with whatever petting responses you like. You can also add more dialogue here if you want, or cut them down. Though removing them entirely seems a bit counter-intuitive to all that work we've been doing.
 			}
 			elseif reference4 == "Face" //Zarla : Another area set up by the collisions in surfaces.txt. You can expand this list with as many elseifs as you need depending on how many collisions you've set up.
 			{
 				if useraffection >= 300
				{
					"\0\i[15]%(lsto)* Huh,\w5 \i[11]sigh...\e"
					"\0\i[15]%(lsto)* Huh,\w5 \i[12]sigh...\e"
					"\0\i[15]%(lsta)Boop !%(lsto)\w5 \n\n[half]\i[11]* Sigh...\e"
					"\0\i[15]%(lsta)Boop !%(lsto)\w5 \n\n[half]\i[12]* Sigh...\e"
					"\0\i[15]%(lsta)Boop, hehe...%(lsto)\w5 \n\n[half]\i[11]* Sigh...\e"
					"\0\i[15]%(lsta)Boop, hehe...%(lsto)\w5 \n\n[half]\i[12]* Sigh...\e"
					"\0\i[13]%(lsta)Boop !%(lsto)\n\n[half]\i[18]* Will you stop one day ?\e"
				}
				elseif useraffection >= 200
				{
					"\0\i[13]%(lsta)Boop !%(lsto)\n\n[half]\i[18]* This noze would like some peace.\e"
					"\0\i[13]%(lsta)Boop !%(lsto)\n\n[half]\i[18]* Can you spare my noze please ?\e"
					"\0\i[13]%(lsta)Boop !%(lsto)\n\n[half]\i[18]* Stop annoying my noze please.\e"
					
				}
				elseif useraffection >= 100
				{
					"\0\i[13]%(lsto)* Huh, \w5that's my noze. \i[18]And it would like you stopping annoying it.\e"
					"\0\i[15]%(lsta)Boop !%(lsto)\i[13]\n\n[half]* Yeah,\w5 that's my noze,\w5 now let it alone.\e"
					"\0\i[13]%(lsta)Boop.\n\n[half]%(lsto)* Why are you touching my noze ?"
				}
				elseif useraffection >= 50
				{
					"\0%(lsto)* Ehh !!! \i[13]\w7Can you avoid doing this please ?\e"
					"\0%(lsto)* Huh, \i[13]Don't touch my noze please.\e"
					"\0%(lsto)\i[13]* Huh,\w5 my noze didn't do anything to you,\w5\i[12] let it alone."
				}
				else
				{					
					"\0%(lsto)* Don't touch me,\w5 you don't know me.\e"
					"\0%(lsto)* Keep your hands for yourself.\e"
				}
 			}
			elseif reference4 == "Obsidian"
			{
				res_reference0 = "Dusty"
				"\0%(lsta)You pet the cat."
				"\0%(lsta)You pet Obsidian"
				"\0%(lsta)You feel purring under your hand."
				"\0%(lsta)Soft cat."
				"\0%(lsta)The fur is soft under your hand."
				--
				"\e AzuraObsiPets"
			}
 		}
	}
 	else
	{
 		//Zarla : If the mouse isn't over the ghost, it'll zero out the stroke value.
		stroke = 0
 	}
}

 //---- MouseMoveKero -----------------------------------------------------------------
 //Zarla : MouseMoveKero is exactly like the above, except set up for your second character. 
 
MouseMoveKero
 {
	if reference4 != ""
 	{
 		stroke++
 		if stroke >= 40
		{
			stroke = 0
			if reference4 == "Face" //Zarla : Put in the name of your collision area you set up for your secondary character here.
 			{
				"\1\s[1]This is a face touching dialogue.\e"
				"\1\s[1]This is another face touching dialogue.\e"
				"\1\s[1]This is yet another face touching dialogue.\e"
				//Replace this dialogue with your secondary character's dialogue.
 			}
			elseif reference4 == "Point" //Like above, put in the name of another collision you set up in surfaces.txt for the secondary character. You can keep adding more elseifs like this for any extra collisions you may have set up.
			{
				"\1\s[1]This is a point touching dialogue since I don't really have a head, being a triangle.\e"
				"\1\s[1]This is another point touching dialogue.\e"
				"\1\s[1]This is yet another point touching dialogue.\e"
				//Replace this dialogue as appropriate.
			}
			
 		}
	}
	elseif reference3 == 1 && nowshell == "Halloween shell"
	{
		stroke ++
		if stroke >= 40
		{
			"\1%(lsta)The pumpkin isn't cold."
			"\1%(lsta)You feel a radiance coming from the pumpkin."
			"\1%(lsta)It's a very weird glowing pumpkin."
			"\1%(lsta)The skin feels like a normal pumpkin."
			stroke = 0
		}
		
	}
 	else
	{
 		// If the mouse isn't over the ghost, it'll zero out the stroke value.
		stroke = 0
 	}
}

//---- OnMouseDoubleClick -------------------------------------------------------
//Zarla : 
//This is set up much like OnMouseMove above, except this is checking to see if the user is double clicking on the ghost. You also don't need to touch any of this. 
//Advanced users adding a third character, the same principles from OnMouseMove apply here as well! Make sure to add an elseif with the right number for your third character.

OnMouseDoubleClick
 {
	//Zarla : ---- like with the OnMouseMove function above, this determines who the user is clicking on.
	
 	if reference3 == 0 //Zarla : if the character is 0, aka your main character
	{
		MouseDoubleClickSakura
 	}
	elseif reference3 == 1 && nowshell == "Halloween shell"
 	{
 		"\1%(lsta)It's a pumpkin."
		"\1%(lsta)You don't dare touching the pumpkin."
		"\1%(lsta)The menacing glow keeps you from punching the pumpkin."
	}
	else
	{
		MouseDoubleClickKero
	}
 }

 //---- MouseDoubleClickSakura ----------------------------------------------------------
 //Zarla : Like the petting animation above, this is the reactions they have to being double clicked. These also rely on the collisions you set up in surfaces.txt! This set is specifically for the main character, as indicated by the Sakura in the name again.
 
 //*********************Advanced User Info*******************************
 //Zarla : 
 //As a note, double clicking on a certain area of your ghost doesn't have to be the user hitting them. You could instead set up a collision for a piece of clothing, and then have the character talk about that bit of clothing when it's double clicked, for example. One of Smoker's double clicks gives her a high-five when she's in a certain pose. Maybe double clicking is patting them, or pointing behind them, or tapping them, or any number of things. You can be creative here with how they respond depending on what areas you've set up as collisions!
 
 //If you don't want your character to be hittable at all, you can just delete everything in MouseDoubleClickSakura except OpenSakuraMenu. So it'd look like
 
 //MouseDoubleClickSakura
 //{
 //OpenSakuraMenu
 //}
 
 //Zarla : That's all there is to it. This method also applies to the secondary character, Kero, below this set.
 //***********************************************************************
 
MouseDoubleClickSakura
 {
 	if reference4 == "Head" || reference4 == "Face" //Zarla : here, this is checking to see if the user is double clicking on either the character's head OR their face, which for our purposes means they're hitting the ghost. More about operators at the walkthrough.
	{ 
		if asleep != 1
		{
			if issat == 1
			{
				if nowshell == "Old shell" && inpyjama == 1
				{
					azpose = "\0\s[20]%(smile)"
				}
				else
				{
					azpose = "\0\s[10]%(smile)"
				}
				"%(azpose)%(arms)\w2"
				issat = 0
			}
			oncomputer = 0
			isdrawing = 0
			if alreadyeat >= 2
			{
				alreadyeat = 1
			}
			--
			"%(lsto)\_v[soul_damage_1.wav]"
			--
			if azpunched <= 0
			{
				"\0\i[13]\i[49]\i[36]\i[37]* Aye !!! \w6\i[18]\i[40]\i[91]Why ?\w7 Why did you do this ?"
			}
			elseif azpunched <= 2
			{
				"\0%(azpose)\i[70]\i[13]\i[49]\i[36]\i[37]\i[179]* Eehhh ! \w6%(azpose)\i[90]\i[70]\i[40]I did nothing bad.\w7 Why are you mean with me ?"
			}
			elseif azpunched <= 4
			{
				"\0\i[40]\i[187]\i[36]\i[37]* Humf !\w6 You really thought I wouldn't protect myself ?\w7 I did nothing wrong so stop punching me please."
			}
			elseif azpunched <= 5
			{
				"\0\i[40]\i[187]\i[36]\i[37]* Huh !\w6 You punched me enough time, \w5stop that."
			}
			elseif azpunched <= 6
			{
				"\0\i[40]\i[187]\i[36]\i[37]* I told you to stop punching me,\w5 my patience have limits."
			}
			elseif azpunched <= 7
			{
				"\0\i[40]\i[187]\i[36]\i[37]* Do you really think you can punch me without any consequences ? \w7Do you think I can't escape ?\w7 Next time you punch me,\w5I go away.\w7 I go back in my world and you wouldn't be able to bring me back here.\w7 You're warned."
			}
			else
			{
				"\0\i[40]\i[187]\i[36]\i[37]* Alright,\w5 you have been warned,\w5 you made your choice. \w7Now you would never see me again.\w7 I go back home.\w7 Good bye forever.\w9\![raise,OnPunchedUninstalled]\e"
			}
			--
			"\e"
			--
			azpunched++
			useraffection -= 20
			DecreasePositiveAction
		}
		else
		{
			"\0%(lsta)You stop yourself before hitting her. That's not the right moment.%(lsto)\e"
		}
		//Zarla : Replace this dialogue with appropriate dialogue as you need. You can add more or cut it down if you want, even down to nothing if you want no response from your ghost at all when those areas are double clicked.
 	}
	else
 	{
		//Zarla : Otherwise, if the user doubleclicks somewhere else on the character, they will open their menu as detailed in menu.dic.
 		OpenSakuraMenu
	}
 }

 
 //---- MouseDoubleClickKero -----------------------------------------------------------
 //Zarla : This is just the same as MouseDoubleClickSakura above, except for your secondary character. The same principles apply, including how to make your character unhittable.
 
 
MouseDoubleClickKero
 {
	 /*
 	if reference4 == "Face" || reference4 == "Head" //Triangle doesn't have a separate head really, being a triangle, so this only happens if you doubleclick his face.
	{
		"\1\s[1]This is hitting dialogue.\w8ha ha\w8\n\n[half]Quiet.\e"
		"\1\s[1]This is another hit dialogue.\e"
		"\1\s[1]This is yet another hit dialogue.\e"
		"\1\s[1]This is the fourth hitting dialogue.\e"
		//Replace the dialogue here for the second character with whatever you want. What I said above about adding or trimming still holds here.
 	}*/
	_kero = "\1"
	if nowshell == "Halloween shell"
	{
		_kero = "\p[2]"
	}
	if reference4 == "Play"
	{
		songpaused = songplayed
		paused = 1
		--
		Select.playmu
	}
	elseif reference4 == "Prev"
	{
		Select.previousmu
	}
	elseif reference4 == "Next"
	{
		Select.nextmu
	}
	elseif reference4 == "Info"
	{
		OnCreateMuInfo
	}
	elseif reference4 == "Playlist"
	{
		OnPlayerPlaylist
	}
	elseif reference4 == "Mini"
	{
		"%(_kero)\s[8]"
	}
	elseif reference4 == "Maxi"
	{
		"%(_kero)\s[5]"
		--
		if isplayed == 1
		{
			"%(_kero)\i[2003]"
		}
	}
	elseif reference4 == "Maximax"
	{
		"%(_kero)\s[2]"
		--
		if isplayed == 1
		{
			"%(_kero)\i[2003]\i[9000]"
		}
	}
	elseif reference4 == "Close"
	{
		boarddisplayed = 0
		"%(_kero)\s[-1]"
	}
	else //Zarla : Otherwise if you click anywhere else on Triangle's body
 	{
		OpenKeroMenu //This will open his menu set up in menu.dic.
	}
 }

 //*********************Advanced User Info*******************************
 //Zarla : There are other mouse related functions you could set up much like the above, such as OnMouseDragStart, OnMouseDragEnd, OnMouseWheel, and others which correspond to various things you can do with your mouse. You can probably guess what those functions do just by their names - OnMouseWheel would be if someone spun their mouse wheel over a certain area, for example. If you're interested in these, I'd recommend doing some reading at the CROW-SSP Encyclopedia to get a better idea of them and settle down for some trial and error learning as you try to put them in.
 //***********************************************************************